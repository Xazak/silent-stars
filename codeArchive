A person can be concussed or sober. A person is usually sober. The player is concussed.

[in order to get a global-level name scrambler, i need to find a way to tell I7 /which/ name (the normal or the scrambled) to put into the "printed name" property, so that other properties that refer to it will properly use the name]

[scramble-normal checks:
-when the player first becomes concussed
-when the player changes rooms
-when the player sobers up
]

Brain Damage is a scene. Brain Damage begins when the player is concussed. Brain Damage ends when the player is sober.

Every thing has some text called the real name. The real name is usually "[printed name]". Every thing has some text called the scramble name. The scramble name is usually "[printed name]".

When Brain Damage begins:
	repeat with item running through the list of things enclosed by the location:
		now the real name of the item is the printed name of the item;
		mangle the name of the item;
		
To mangle the name of (item - a thing) (this is mangling):
	let alias be the printed name of the item;
	let order-template be a list of numbers;
	let new-word be an indexed text;
	let final-replacement be an indexed text;
	let final-replacement be the alias;
	repeat with foo running from 1 to the number of words in the alias:
		let word-bucket be word number foo in the alias;
		let new-word be word-bucket;
		let word-length be the number of characters in word-bucket;
		now order-template is { }; [make sure the template is empty]
		repeat with bar running from 1 to word-length: [populate the template with the length of the word]
			add bar at entry 1 in order-template;
		sort order-template in random order; [scramble the template]
		if word-length is at least 4:
			if entry 1 in order-template is not 1:
				remove 1 from order-template;
				add 1 at entry 1 in order-template;
			if entry word-length in order-template is not word-length:
				remove word-length from order-template;
				add word-length to order-template;
		repeat with baz running from 1 to word-length:
			let template-reference be entry baz in order-template;
			replace character number baz in new-word with character number template-reference in word-bucket;
		let new-word be "![new-word]"; [NFR]
		replace word number foo in final-replacement with new-word;
	now the scramble name of the item is "[final-replacement]";
	
***
Dreamspace is a room. The printed name of Dreamspace is "Kitchen". Understand "kitchen" as Dreamspace. "Silk curtains, dark wood cabinets, matte grey hardware: you're in the kitchen of your family home. No matter how much time you spend in your workshop or the little office in the hut out back, this is where you truly feel you're at rest."

Dreamspace is lighted.

A container called the curtains is here. The curtains are fixed in place, transparent, closed and openable. "[if closed]Thin handwoven silk curtains shroud a couple of blurred shapes moving around the yard outside.[otherwise if open]A pair of smart woven silk curtains frame the kitchen window; in the yard outside, Victor and his friend play at being soldiers.[end if]"

Understand "curtain" as the curtains.

A window is inside the curtains. The window is fixed in place, bright, lit. The description is "Outside, you can see your son Victor playing in the garden with the neighbor boy. They're pretending to be soldiers, creeping around and through the plants on belly and forearms, evading imaginary machine-gun fire. Maxie hates this game, says it'll make Victor too aggressive when he's older, but you never chastise Victor for it. [i]Anything[/i] to get him to play outside."

Before examining the window while the curtains are closed: 
	try opening the curtains;
	continue the action.

Report opening the curtains: say "You open the curtains, tying them back with the little cord so they hang neatly." instead.

Some cabinets are here. The cabinets are scenery. The description is "The cabinets were Maxie's choice: maple, stained in a dark rocky brown, with sterling silver hardware. Not your first choice, but now you can't imagine a kitchen without them. Max always [i]was[/i] better at color coordination."

Understand "cabinet" or "cupboard" as the cabinets.

Instead of opening the cabinets: say "You reach up to open the closest cupboard, but stop halfway. You can't remember what you needed out of there in the first place."

A device called a gas stove is here. The stove is fixed in place. "A gas range, showpiece of the kitchen and the center of your food obsessions, claims center stage in the middle of the kitchen island[if teapot is on the burner]. A bright red teapot rests on the stove[end if][if the stove is hot]. The flame under the burner is lit[end if]." The description is "It's a gas range, installed at great expense and peril. Being part of the Europa Engineer's Scholarship affords certain benefits and special privileges; one must take advantage of them when one can." The stove is hot.

Carry out switching on the stove: now the stove is hot.
Report switching on the stove: say "The igniter ticks a few times and the flame catches with a quiet huff."
Carry out switching off the stove: now the stove is cold.
Report switching off the stove: say "The flame goes out."

Understand "flames" or "flame" as the burner.

A burner is part of the stove. The description is "You always liked the blue hue of those flames."

A red teapot is on the burner. "The hot water left in the teapot is still warm enough to let off some steam." The description is "A cherry-red teapot, shaped like a sphere of jello resting on a plate. Not the ugliest appliance in here, but definitely not the prettiest." The teapot is notable, cold.
[The sound is "whistling". The sound-description is "A thin, high keening whistle, much more shrill than a train engine. It's loud enough to throb in your ear, on the edge of painful."]

Instead of doing something with the cold teapot: say "Can't brew coffee with cold water. That is, not if you want to drink it the same day. Best to leave it be."

The kitchen counter is here. "A [if onion is unpeeled]un[end if]peeled [white onion] lies on the kitchen counter." The description is "Max picked out the slate- and charcoal-colored tiles, but the countertop itself is stainless steel. It was one of the few things you insisted on when putting together the kitchen decor."

A white onion is on the counter. The description is "A run-of-the-mill white onion, about as big as your clenched fist." An onion can be unpeeled or peeled. An onion is unpeeled. An onion can be chopped or uncut.

[Understand the command "peel the [thing]" as peeling. ]

A man called Max is here. "Max is chopping celery." 
Understand "Maxie" as Max.

When Dreaming begins:
	Max finishes the celery in one turn from now;

At the time when Max finishes the celery:
	say "Max piles the cut celery into a small bowl and sets it to one side, looking for something.";
	Max asks for the onion in one turn from now;
	
At the time when Max asks for the onion:
	say "Max notices the onion by you and brightens. 'Would you mind handing me the onion? I left it over there by mistake.' A sardonic grin flits across his face. 'One of these days I might get to be as prepared as you.'";

After giving the onion to Max:
	if the onion is peeled:
		say "Max smiles and says 'Aw, thanks. You didn't have to.'";
		Max chops the onion in one turn from now;
	otherwise:
		say "'Thanks, hon.' He picks his knife back up and slices off the top and bottom of the onion.";
		Max peels the onion in one turn from now;
		
At the time when Max peels the onion:
	say "Max slices the onion in half and pulls the skins off, then sets to with the knifeblade. He still needs practice; those years as an artist didn't come with cooking lessons.";
	Max chops the onion in one turn from now;
	
At the time when Max chops the onion:
	say "Max takes the diced onion and mounds it into another waiting bowl.";
	Max shows the onion in one turn from now;

At the time when Max shows the onion:
	say "He holds the bowl out to you. 'Could you take a look at this, see how I did?'";

[TURN SEQUENCE:
Turn 1: Player enters game world, assesses situation
Turn 2: Max finishes chopping a vegetable .. max-finish-celery
Turn 3: Max asks the player for the onion .. max-ask-onion
Turn 4: Max peels the onion .. max-peel-onion
Turn 5: Max begins chopping the onion .. max-chop-onion
Turn 6: Max finishes chopping the onion .. max-finish-onion
Turn 7: Max tries to show the player the diced vegetables .. max-show-onion

Conditions for exiting the Dreamscape:
A) The player must have been wounded by Max's knife
B) The teapot must have started to make noise

Contents of the kitchen:
X	Curtains
X	--A window
X	Cabinets
?	Porcelain sink
X	Gas stove
X	--A whistling teapot
	Maxie, chopping vegetables
	Victor (outside), playing with the neighbor boy
	the neighbor boy
]

Dreaming is a scene. Dreaming begins when the player is asleep. Dreaming ends when the player is lucid.

A scene can be top or bottom. Dreaming is top.

To decide if the player is lucid:
	if the teapot is audible:
		if the player is wounded, yes;
	no;
	
[The description is "Maxie was the one who picked out the slate-and-charcoal tesselated tiles on the floor, the mocha-cream walls and chocolate trim, the matching porcelain sink, but the stainless steel surfaces and ancient gas range were [i]your[/i] idea."]
***********************

A puddle is here. The puddle is scenery. The description of the puddle is "[description of the water]". Understand "water" as the puddle when the player is in the rocky shore line.

Down from a rocky shore line is the water. The water is in Dreamspace. "Still water cools the air.[br]It seems to stretch forever.[br]No waves touch your feet."
The dark-description is "[description]";

Before doing something with the water:
	if the current action is examining, continue the action;
	try examining the water instead;

The dreamtime-containers-exception rule is listed instead of the examine containers rule in the carry out examining rules.
This is the dreamtime-containers-exception rule:
	if the player is in Dreamspace:
		do nothing;
	otherwise:
		abide by the examine containers rule;



Rule for reaching inside while the player is in Dreamspace:
	if the player is not in the water:
		say "[first time]You crouch down and reach.[no line break][br]
		It's like bare skin in deep space.[no line break][br]
		Sensation departs.[br][only]
		Struggling brings nothing.[no line break][br]
		To reach it, you must dive in.[no line break][br]
		Commit all, or none.";
		deny access;

Understand "DIVE IN" as diving.
Understand "JUMP IN" as diving.

Diving is an action applying to nothing.
Check diving:
	if the location is not Dreamspace, say "Can't do that here bro." instead;

Before diving:
	if the location is Dreamspace:
		say "One breath, plus one held.[no line break][br]
		Even your bones feel like ice.[no line break][br]
		Darkness shrouds your thoughts."
	
Carry out diving:
	now the player is inside the water;

[The first time the player enters the bay: "When you lift your head to look around, a bright flash of panic runs through your skull: your right arm is scarlet red from fingertips to elbow. The autodoc's drug-induced haze parts, and as you lift your arm, you realize there is a [i]massive[/i] pool of blood on the floor.";]

[the first time the player visits the Medical Bay from the autodoc/when autodoc escape ends?:
	say "You make a [i]splash[/i] instead of a [i]thud[/i] when you land. As you raise your drug-addled head to look, you choke down the urge to vomit as you realize the syrupy black gunk coating your arms up to the bicep is half-dried blood. The puddle is almost ankle-deep and wall-to-wall."]
